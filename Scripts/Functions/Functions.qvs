//Verifica se um arquivo existe. P1 = arquivo, caminho + nome
SET FileExists = fabs((not IsNull(FileTime($1))));

//Criar um Id composto.
SET SetId = IF(IsNull($2), -1, IF(IsNull($3), $1 & '|' & $2, $1 & '|' & $3 & '|' & $2));

//Transformar um texto em formato de CPF, CNPJ ou CEP
SET OnlyNumbers = KeepChar($1, 0123456789);

SET RepeatZeros = Pick(WildMatch($2, 'F', 'J', 'CEP')
    , Repeat(0, 11 - Len($(OnlyNumbers($1))))
    , Repeat(0, 14 - Len($(OnlyNumbers($1))))
    , Repeat(0, 8 - Len($(OnlyNumbers($1))))
) & $(OnlyNumbers($1))
;

SET FormatarCPF = 
	Mid($(RepeatZeros($1, 'F')), 1, 3) & '.' & 
    Mid($(RepeatZeros($1, 'F')), 4, 3) & '.' & 
    Mid($(RepeatZeros($1, 'F')), 7, 3) & '-' & 
    Mid($(RepeatZeros($1, 'F')), 10, 2)
;
SET FormatarCNPJ = 
	Mid($(RepeatZeros($1, 'J')), 1, 2) & '.' & 
    Mid($(RepeatZeros($1, 'J')), 3, 3) & '.' & 
    Mid($(RepeatZeros($1, 'J')), 6, 3) & '/' & 
    Mid($(RepeatZeros($1, 'J')), 9, 4) & '-' & 
    Mid($(RepeatZeros($1, 'J')), 13, 2)
;

SET FormatarCEP = 
	Mid($(RepeatZeros($1, 'CEP')), 1, 2) & '.' & 
    Mid($(RepeatZeros($1, 'CEP')), 3, 3) & '-' & 
    Mid($(RepeatZeros($1, 'CEP')), 6, 3)
;

//Coloca o texto entre aspas
SET EntreAspas = Chr(39) & $1 & Chr(39);